apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'

jar.baseName = 'releaseNotes'
group = 'com.spidasoftware'
version = '0.1-SNAPSHOT'
sourceCompatibility = 1.7
targetCompatibility = 1.7

def m2Settings = {
	def root = new XmlSlurper().parse(new File(System.getProperty("user.home"), ".m2/settings.xml"))
	def server = root.servers.server.find{ it.id.text() == "spida" }
	def repos = root.profiles.profile.find{ it.id.text() == "artifactory" }.properties
	[	username: server.username.text(),
		password: server.password.text(),
		exposed: repos.exposedLocation.text(),
		snapshots: repos.snapshotsLocation.text(),
		releases: repos.releasesLocation.text()		]
}()

repositories {
	mavenCentral()
	maven { url m2Settings.exposed }
}

uploadArchives {
	repositories.mavenDeployer {
		repository(url: m2Settings.exposed) {
			authentication(userName: m2Settings.username, password: m2Settings.password)
		}
	}
}

configurations {
	deployerJars
}

// change gradle to automatically pull the newest version of all snapshot dpendencies marked "changing"
configurations.all {
	resolutionStrategy.cacheChangingModulesFor 0, 'hours'
}

test {
	testLogging.showStandardStreams = true
}

dependencies {
	compile gradleApi()
	compile localGroovy()
	compile group:'org.codehaus.groovy.modules.http-builder', name:'http-builder', version: '0.7'
    testCompile group: 'junit', name: 'junit', version: '4.11'
	deployerJars "org.apache.maven.wagon:wagon-http:2.2"

}
