apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'

jar.baseName = 'releaseNotes'
group = 'com.spidasoftware'
version = '0.1-SNAPSHOT'
sourceCompatibility = 1.7
targetCompatibility = 1.7

try {
	def m2Root = new XmlSlurper().parse(new File(System.getProperty("user.home"), ".m2/settings.xml"))
	def m2Spida = m2Root.servers.server.find{ it.id.text() == "spida" }
	def m2Repos = m2Root.profiles.profile.find{ it.id.text() == "artifactory" }.properties
	project.ext.m2Settings = [username: m2Spida.username.text(), password: m2Spida.password.text(), exposed: m2Repos.exposedLocation.text()]
} catch(ex){
	println ex.message //use ex.printStackTrace() if you want to show the full stacktrace 
	project.ext.m2Settings = [exposed:"https://dev.spidasoftware.com/artifactory/exposed-repo"]
}


repositories {
	mavenCentral()
	maven { url m2Settings.exposed }
}

uploadArchives {
	repositories.mavenDeployer {
		repository(url: m2Settings.exposed) {
			authentication(userName: m2Settings.username, password: m2Settings.password)
		}
	}
}

configurations {
	deployerJars
}

// change gradle to automatically pull the newest version of all snapshot dpendencies marked "changing"
configurations.all {
	resolutionStrategy.cacheChangingModulesFor 0, 'hours'
}

test {
	testLogging.showStandardStreams = true
}

dependencies {
	compile gradleApi()
	compile localGroovy()
	compile group:'org.codehaus.groovy.modules.http-builder', name:'http-builder', version: '0.7'
    testCompile group: 'junit', name: 'junit', version: '4.11'
	deployerJars "org.apache.maven.wagon:wagon-http:2.2"

}
