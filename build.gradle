apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'

jar.baseName = 'releaseNotes'
group = 'com.spidasoftware'
version = '0.1-SNAPSHOT'
sourceCompatibility = 1.7
targetCompatibility = 1.7

def getMavenSettings() {
	// Read artifactory username and password from .m2/settings.xml (gradle does not do this)
	File mavenSettings = new File(System.getProperty("user.home"), ".m2/settings.xml")
	def settings = [:]
	if(mavenSettings.exists()){
		def root = new XmlSlurper().parse(mavenSettings)
		def serverElement = root."servers"."server".find{ it."id".text() == "spida" }
		settings.username = serverElement.username.text()
		settings.password = serverElement.password.text()
	}
	return settings
}

repositories {
	mavenCentral()
	maven { url "https://www.spidasoftware.com/artifactory/exposed-repo" }
}

uploadArchives {
	def mavenSettings = getMavenSettings()
	repositories.mavenDeployer {
		repository(url: "https://dev.spidasoftware.com/artifactory/exposed-repo") {
			authentication(userName: mavenSettings["username"], password: mavenSettings["password"])
		}
	}
}

configurations {
	deployerJars
}

// change gradle to automatically pull the newest version of all snapshot dpendencies marked "changing"
configurations.all {
	resolutionStrategy.cacheChangingModulesFor 0, 'hours'
}

test {
	testLogging.showStandardStreams = true
}

dependencies {
	compile gradleApi()
	compile localGroovy()
	compile group:'org.codehaus.groovy.modules.http-builder', name:'http-builder', version: '0.7'
    testCompile group: 'junit', name: 'junit', version: '4.11'
	deployerJars "org.apache.maven.wagon:wagon-http:2.2"

}
